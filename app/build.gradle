

apply plugin: 'com.android.application'

// Only apply the google-service plugin if the config file exists
if (file('google-services.json').exists()) {
    apply plugin: 'com.google.gms.google-services'
}

android {
    compileSdkVersion 24
    buildToolsVersion '26.0.2'
    flavorDimensions "default"

    signingConfigs {
        release {
            def keystore = file('keystore.properties');
            if (keystore.exists()) {
                def Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(keystore))
                storeFile file(keyProps["store"])
                storePassword keyProps["storePass"]
                keyAlias keyProps["alias"]
                keyPassword keyProps["pass"]
            }
        }
    }

    defaultConfig {
        applicationId 'Converter'
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 50200
        versionName '5.2'
    }


    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        base {
            // Basic build
        }
        google {
            // Adds the option to donate to the developer
            // Adds Firebase crash analytics
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.android.support:recyclerview-v7:24.2.0'
    compile 'com.android.support:design:24.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.3'
    googleCompile 'com.google.firebase:firebase-crash:10.2.1'
    debugCompile 'com.facebook.stetho:stetho:1.1.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.0'
}
